# Maintainer: tkit <ch1994@outlook.com>
pkgname=emacs-nautilus-git
pkgver=r9.ddff975
pkgrel=2
pkgdesc="Emacs extension for Nautilus"
arch=('i686' 'x86_64')
url="https://github.com/Rybski/emacs-nautilus"
license=('custom')
depends=('python-nautilus')
makedepends=('git')
optdepends=('emacs: Emacs editor')
install="${pkgname}.install"
source=("${pkgname}::git+${url}.git")
md5sums=('SKIP')
pkgver() {
    cd "$pkgname"
    (
        set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
            printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

package() {
    cd "$pkgname"
    install -Dm755 emacs-nautilus.py "$pkgdir/usr/share/nautilus-python/extensions/emacs-nautilus.py"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
